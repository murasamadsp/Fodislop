#ifndef KERN_LIGHTING_TYPES_HLSL
#define KERN_LIGHTING_TYPES_HLSL

// Общие типы, константы и packing/unpacking функции для всей lighting-системы.
//
// ЭТОТ ФАЙЛ НЕ ДОЛЖЕН ЗНАТЬ ни о каскадах, ни об источниках.
// Он содержит только базовые типы и константы, которые используются всеми стадиями.

#define PI 3.14159265359
#define PI2 6.28318530718

// Angular horizon slack (TraceDynamicPolar / DynamicHorizonContains). The far
// gather subtracts the optical depth at the ray's entry into the source cell,
// at most this many solid cells; one source chord weighs below this factor.
// Derived bounds, not tuning values.
static const float DynamicHorizonSourceDepthCells = 1.5;
static const float DynamicHorizonWeightMargin = 1.5;

// Плотность сэмплирования эмиттера по оси — юниформа
// _DynamicEmitterPointsPerAxis, объявленная в WorldLighting.compute.
// Авторское значение живёт в VisualTuning.cs.

uint2 PackRadiance(float3 radiance)
{
    return uint2(
        f32tof16(radiance.r) | (f32tof16(radiance.g) << 16),
        f32tof16(radiance.b));
}

float3 UnpackRadiance(uint2 packedRadiance)
{
    return float3(
        f16tof32(packedRadiance.x & 0xffff),
        f16tof32(packedRadiance.x >> 16),
        f16tof32(packedRadiance.y & 0xffff));
}

uint3 PackInterval(float3 radiance, float3 transmittance)
{
    return uint3(
        f32tof16(radiance.r) | (f32tof16(radiance.g) << 16),
        f32tof16(radiance.b) | (f32tof16(transmittance.r) << 16),
        f32tof16(transmittance.g) | (f32tof16(transmittance.b) << 16));
}

float3 UnpackTransmittance(uint3 packedInterval)
{
    return float3(
        f16tof32(packedInterval.y >> 16),
        f16tof32(packedInterval.z & 0xffff),
        f16tof32(packedInterval.z >> 16));
}

struct DynamicLight
{
    float4 positionRadius;
    float4 colorIntensity;
};

struct DynamicTileInfo
{
    int2 fieldOrigin;
    int2 size;
    int2 tileOffset;
    int reachIndex;
    int reserved;
};

// One source dispatch, mirrored by DynamicLightBatch.WorkItem (32 bytes).
// Receiver origin/size use the light lattice; polar size is angles/radial rows.
struct DynamicLightWorkItem
{
    int2 receiverOrigin;
    int2 receiverSize;
    int2 polarSize;
    int lightIndex;
    int slot;
};

// Explicit per-invocation state shared by serial and batched transport.
struct DynamicTraceContext
{
    int2 receiverOrigin;
    int2 receiverSize;
    int2 tileOffset;
    int2 polarSize;
    int lightIndex;
    int slot;
    int polarLayerOffset;
    int horizonBase;
};

#endif // KERN_LIGHTING_TYPES_HLSL
